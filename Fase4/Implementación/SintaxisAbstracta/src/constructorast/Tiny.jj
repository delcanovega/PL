options  {
  STATIC=false;
}  
PARSER_BEGIN(ConstructorASTTiny)
package constructorast;

import ast.*;


public class ConstructorASTTiny {
   private ASTiny as = new ASTiny();
   private final static int MAYOR=0;
   private final static int MENOR=1;
   private final static int MAYORIGUAL=2;
   private final static int MENORIGUAL=3;
   private final static int EQUIV=4;
   private final static int NOEQUIV=5;
   private E mkexp(int op, E opnd1, E opnd2){
     switch(op) {
      case MENOR: return as.menor(opnd1,opnd2);
      case MAYOR: return as.mayor(opnd1,opnd2);
      case MENORIGUAL: return as.menorIgual(opnd1,opnd2);
      case MAYORIGUAL: return as.mayorIgual(opnd1,opnd2);
      case EQUIV: return as.equiv(opnd1,opnd2);
      case NOEQUIV: return as.noEquiv(opnd1,opnd2);
      default: return null;
     }
   }
}
PARSER_END(ConstructorASTTiny)
  TOKEN:{<#letra:["a"-"z","A"-"Z"]>}
  TOKEN:{<#digito:["0"-"9"]>}
  TOKEN:{<#parteDecimal: "."(<digito>)+>}
  TOKEN:{<#parteExponencial: ("e"|"E")(["+","-"])?(<digito>)+>}
  SKIP:{<["\t"," ","\r","\b","\n"]>}  
  SKIP:{<"#"(~["\n"])*>}
  TOKEN:{<num: "num">}
  TOKEN:{<bool: "bool">}
  TOKEN:{<ttrue: "true">}
  TOKEN:{<ffalse: "false">}
  TOKEN:{<and: "and">}
  TOKEN:{<or: "or">}
  TOKEN:{<not: "not">}
  TOKEN:{<identificador:<letra>(<letra>|<digito>|"_")*>}
  TOKEN:{<numero: (["+","-"])?(<digito>)+(<parteDecimal>)?(<parteExponencial>)?>}
  
  S Sp()    : {S ast;} {ast=S() <EOF> {return ast;}}
  S S()		: {ResulDs ds; ResulIs is;} {ds = Ds() "&&" is=Is() {return as.decIns(ds.a(), is.a());}}	
  
  ResulDs Ds()	: {ResulD d; Ds fd;} {d = D() fd = FD(as.dSimple(d.id(), d.tipo())) {return new ResulDs(fd);}}
  Ds FD(Ds ds)	: {ResulD d; Ds fd;} {";" d = D() fd = FD(as.dCompuesta(ds, d.id(), d.tipo())) {return fd;} |
  									{return ds;}}
  ResulD D(): {Token iden; T t;} {t = T() iden = <identificador> {return new ResulD(iden.image, t);}}
  T T()		: {Token t;} {t=<bool> {return new TBool();} |
   						t=<num> {return new TNum();}}
   						
  ResulIs Is()	: {ResulI i; Is fi;} {i = I() fi = FI(as.iSimple(i.id(), i.exp())) {return new ResulIs(fi);}}
  ResulI I()	: {Token iden; E e;} {iden=<identificador> "=" e=E0() {return new ResulI(iden.image, e);}}
  Is FI(Is is)	: {ResulI i; Is fi;} {";" i = I() fi = FI(as.iCompuesta(is, i.id(), i.exp())) {return fi;} |
  									{return is;}}
  									 									
  E E0()	: {E e1, e0;} {e1 = E1() e0 = RE0(e1) {return e0;}}
  E RE0(E e): {E e1, e0;} {"+" e1 = E1() e0 = RE0(as.suma(e, e1)) {return e0;} |
  							"-" e1 = E1() e0 = RE0(as.resta(e, e1)) {return e0;} |
  							{return e;}}
  E E1()	: {E e2, e1;} {e2 = E2() e1 = FE1(e2) {return e1;}}
  E FE1(E e): {E e2;} {"and" e2 = E1() {return as.and(e, e2);} |
  						"or" e2 = E2() {return as.or(e, e2);} |
  						{return e;}}
  E E2()	: {E e3, e2;} {e3 = E3() e2 = FE2(e3) {return e2;}}
  E FE2(E e): {E e3; int op;} {op = OP() e3 = E3() {return mkexp(op, e, e3);} |
  								{return e;}}
  E E3()	: {E e4, e3;} {e4 = E4() e3 = RE3(e4) {return e3;}}
  E RE3(E e): {E e4, e3;} {"*" e4 = E4() e3 = RE3(as.mul(e, e4)) {return e3;} |
  							"/" e4 = E4() e3 = RE3(as.div(e, e4)) {return e3;} |
  							{return e;}}
  E E4()	: {E e;} {"-" e = E4() {return as.neg(e);} |
  						"not" e = E5() {return as.not(e);} |
  						e = E5() {return e;}}
  E E5()	: {Token t; E e0;} {"(" e0 = E0() ")" {return e0;} |
  								t = <identificador> {return as.id(t.image);} |
  								t = <numero> {return as.numReal(t.image);} |
  								t = <ttrue> {return as.eTrue();} |
  								t = <ffalse> {return as.eFalse();}}							
  int OP()	: {} {"<" {return MENOR;} |
  					">" {return MAYOR;} |
  					"<=" {return MENORIGUAL;} |
  					">=" {return MAYORIGUAL;} |
  					"==" {return EQUIV;} |
  					"!=" {return NOEQUIV;}}							
